-- Some examples of GFF computation taken from
-- 'Greatest-Factorial Factorisation and Symbolic Summation'
-- Peter Paule, J. Symb. Comp. 11 (1996)


Z ==> Integer
Q ==> Fraction(Z)
A ==> AlgebraicNumber
PZ ==> Polynomial(Z)
PQ ==> Polynomial(Q)
PA ==> Polynomial(A)
EZ ==> PolynomialShift(Z)
EQ ==> PolynomialShift(Q)
EA ==> PolynomialShift(A)


-- P.P: Example 2.1
p1 : PZ := x^5 + 2*x^4 - x^3 - 2*x^2
gffp1 := gff(p1)
gffp1
q1 := reduce(*, [fallingFactorial(gffp1(i), x, i)$EZ for i in 1..(#gffp1)])
q1 - p1
-- [x, 1, 1, x + 2]


-- P.P: Page 6
p2 : PZ := x^14 - 2*x^13 + 4*x^12 - 2*x^11 -2*x^10 + 10*x^9 - 16*x^8 + 2*x^7 + 5*x^6 -16*x^5 + 20*x^4 +8*x^3 - 12*x^2
gffp2 := gff(p2)
gffp2
q2 := reduce(*, [fallingFactorial(gffp2(i), x, i)$EZ for i in 1..(#gffp2)])
q2 - p2
--   4    2       2       2
-- [x  + x  - 2, x  + 2, x  + 2x + 1]


-- PP, example 3.2
p3 : PQ := x^2 * (x-1/3) * (x-1/2) * (x-1) * (x-7/3)^3 * (x-3)^2
gffp3 := gff(p3)
gffp3
q3 := reduce(*, [fallingFactorial(gffp3(i), x, i)$EQ for i in 1..(#gffp3)])
q3 - p3
--   8   83  7   235  6   12527  5   30715  4   53347  3   3577  2   343
-- [x  - -- x  + --- x  - ----- x  + ----- x  - ----- x  + ---- x  - --- x,x]
--        6       3         54         81        162        27        18


a : A := sqrt(2)
p4 : PA := (x - a)
gffp4 := gff(p4)
q4 := reduce(*, [fallingFactorial(gffp4(i), x, i)$EA for i in 1..(#gffp4)])
q4 - p4
--       +-+
-- [x - \|2 ]


p5 : PA := (x - a) * shift((x-a),x,-1)$EA
gffp5 := gff(p5)
q5 := reduce(*, [fallingFactorial(gffp5(i), x, i)$EA for i in 1..(#gffp5)])
q5 - p5
--          +-+
-- [1, x - \|2 ]


p6 : PA := (x - a) * shift((x-a),x,-1)$EA  * shift((x-a),x,-2)$EA  * shift((x-a),x,-3)$EA
gffp6 := gff(p6)
q6 := reduce(*, [fallingFactorial(gffp6(i), x, i)$EA for i in 1..(#gffp6)])
q6 - p6
--                +-+
-- [1, 1, 1, x - \|2 ]